["VBR shader for DM106"]
pipeline
<
	version = "1.1.2" 
	author = "liuyu/xuzhuoliang" 
	category = "common" 
	company = "netease"
	merge = "1"
>
{
	include(shader\\including\\envir_uniform.spzs)
	include(shader\\including\\pbr_include_function.spzs)

	["是否启用场景转置"]
	eitheror TRANS_ENABLE;
	macro(TRANS_ENABLE)
	<
		customizable = "1"
		default = "0"
	>;

	["检查AO"]
	eitheror AO_CHECK;
	macro(AO_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["检查diffuse"]
	eitheror DIFFUSE_CHECK;
	macro(DIFFUSE_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["检查金属度"]
	eitheror METAL_CHECK;
	macro(METAL_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["检查粗糙度"]
	eitheror ROUGHNESS_CHECK;
	macro(ROUGHNESS_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["开启环境反射(ENVIR_ENABLE)"]
	eitheror ENVIR_ENABLE;

	["开启法线贴图(XNORMAL_MAP_ENABLE)"]
	eitheror XNORMAL_MAP_ENABLE;
	
	["补光"]
	eitheror DIR_AMBIENT_ENABLE;

	["开启自发光"]
	eitheror SELF_BLING;

	["接受点光"]
	eitheror POINT_LIGHT_ENABLE;

	// ["开启羽毛反射"]
	// eitheror FUR_COLOR_ENABLE;

	["是否开启Oren-nayar因子"]
	eitheror ORENNAYAR_ENABLE;
	macro(ORENNAYAR_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

	// ["使用溶解效果"]
	// eitheror DISSOLVE_ENABLE;
	// macro(DISSOLVE_ENABLE)
	// <
	// 	default = "0"
	// 	customizable = "1"
	// >;

	// ["使用柔边溶解"]
	// eitheror SOFTEDGE_ENABLE;
	// macro(SOFTEDGE_ENABLE)
	// <
	// 	default = "0"
	// 	customizable = "1"
	// >;
	["开启网格闪烁消融"]
	eitheror WEB_SHINING_ENABLE;
	macro(WEB_SHINING_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	macro(SHADOW_MAP_ENABLE)
	<
		default = "1"
	>;
	
	macro(POINT_SHADOW_MAP_ENABLE)
	<
		default = "0"
	>;
	
	macro(XNORMAL_MAP_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

	macro(ENVIR_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

	macro(DIR_AMBIENT_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	macro(SELF_BLING)
	<
		default = "0"
		customizable = "1"
	>;

	macro(POINT_LIGHT_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	// macro(FUR_COLOR_ENABLE)
	// <
	// 	default = "0"
	// 	customizable = "1"
	// >;

	// 贴图
	["漫反射贴图"]
	uniform sampler Tex0 : DIFFUSE
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;
	
	["金属度/sss/光滑度"]
	uniform sampler SamplerRough
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;

	["法线贴图"]
	uniform sampler SamplerNormalMap
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;
	["扰动贴图"]
	uniform sampler _DistortMap
	<
		artist = "1"
		mag = "LINEAR"
		mip = "LINEAR"
		min = "LINEAR"
		path = "fx\\texture\\noise\\sf_noise_clouds_01.png"
	>;
	["流光纹理(R流光G扰动B变色)"]
	uniform sampler _EmissTex
	<
		artist = "1"
		mag = "LINEAR"
		mip = "LINEAR"
		min = "LINEAR"
		// path = "fx\\model\\1009_jingqiduizhang\\noise_semaelectro4.png"
	>;
	["Ramp"]
	uniform sampler _Ramp
	<
		artist = "1"
		mag = "LINEAR"
		mip = "LINEAR"
		min = "LINEAR"
		addressu = "clamp"
		addressv = "clamp"
		path = "common\\textures\\ramp\\ramp01.png"
	>;
	["RampMask"]
	uniform sampler _RampMask
	<
		artist = "1"
		mag = "LINEAR"
		mip = "LINEAR"
		min = "LINEAR"
		path = "fx\\texture\\noise\\noiseclouds01.png"
	>;
	["阴影贴图"]
	uniform highp samplershadow SMAP : SHADOW
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		addressu = "clamp" 
		addressv = "clamp" 
	>;
	
	// —————————————————— PBR调整 ——————————————————
	["手动金属度"]
	uniform lowp float manul_metal
	<
		uigroup = "1 PBR"
		uistep = "1"
		artist = "1" 
		color = "1"
		min = "0.0"
		max = "1.0"
	>;
	
	["手动光滑度"]
	uniform lowp float manul_rough
	<
		uigroup = "1 PBR"
		uistep = "1"
		artist = "1" 
		color = "1"
		min = "0.0"
		max = "1.0"
	>;
	["金属度缩放"]
	uniform lowp float metal_multi
	<
		uigroup = "1 PBR"
		artist = "1" 
		init = "0.0"
		uistep = "0.001"
		min = "-1.0"
		max = "1.0"
	>;
	["粗糙度缩放"]
	uniform lowp float rough_multi
	<
		uigroup = "1 PBR"
		artist = "1" 
		init = "0.0"
		uistep = "0.001"
		min = "-1.0"
		max = "1.0"
	>;
	
	// // ——————————————————变色—————————————————————————
	// ["第一层颜色A"]
	// uniform lowp float4 changecolor1
	// <
	// 	artist = "1"
	// 	init = "(1.0,1.0,1.0,1.0)"
	// 	color = "1"
	// 	uigroup = "变色"
	// >;

	// ["第一层颜色B"]
	// uniform lowp float4 changecolor2
	// <
	// 	artist = "1"
	// 	init = "(0.5,1.0,1.0,1.0)"
	// 	color = "1"
	// 	uigroup = "变色"
	// >;

	// ["第二层颜色A"]
	// uniform lowp float4 changecolor3
	// <
	// 	artist = "1"
	// 	init = "(1.0,1.0,1.0,1.0)"
	// 	color = "1"
	// 	uigroup = "变色"
	// >;

	// ["第二层颜色B"]
	// uniform lowp float4 changecolor4
	// <
	// 	artist = "1"
	// 	init = "(0.5,1.0,1.0,1.0)"
	// 	color = "1"
	// 	uigroup = "变色"
	// >;

	// ["第三层颜色A"]
	// uniform lowp float4 changecolor5
	// <
	// 	artist = "1"
	// 	init = "(1.0,1.0,1.0,1.0)"
	// 	color = "1"
	// 	uigroup = "变色"
	// >;

	// ["第三层颜色B"]
	// uniform lowp float4 changecolor6
	// <
	// 	artist = "1"
	// 	init = "(0.5,1.0,1.0,1.0)"
	// 	color = "1"
	// 	uigroup = "变色"
	// >;

	// ["第一层(A金属度、A粗糙度、B金属度、B粗糙度)"]
	// uniform lowp float4 change_adj1
	// <
	// 	artist = "1"
	// 	init = "(0,0,0,0)"
	// 	max = "1.0"
	// 	min = "-1.0"
	// 	uigroup = "变色"
	// >;

	// ["第二层(A金属度、A粗糙度、B金属度、B粗糙度)"]
	// uniform lowp float4 change_adj2
	// <
	// 	artist = "1"
	// 	init = "(0,0,0,0)"
	// 	max = "1.0"
	// 	min = "-1.0"
	// 	uigroup = "变色"
	// >;

	// ["第三层(A金属度、A粗糙度、B金属度、B粗糙度)"]
	// uniform lowp float4 change_adj3
	// <
	// 	artist = "1"
	// 	init = "(0,0,0,0)"
	// 	max = "1.0"
	// 	min = "-1.0"
	// 	uigroup = "变色"
	// >;	


	// ["曝光度"]
	// uniform mediump float expose_bias
	// <
	// 	uigroup = "1 PBR"
	// 	artist = "1" 
	// 	init = "1.0"
	// 	uigroup = "1 环境光"
	// >;

	["天光颜色"]
	uniform lowp float4 sky_light_color
	<
		uigroup = "1 环境光"
		artist = "0" 
		color = "1"
		init = "(0.5569,0.7412,1,1)"
	>;

	["天光强度"]
	uniform float sky_light_intensity
	<
		uigroup = "1 环境光"
		artist = "0" 
		init = "1.0"
	>;
	
	
	// ——————————————————自发光——————————————————
	
	["自发光强度"]
	uniform mediump float emissive_intensity
	<
		uigroup = "风格化"
		artist = "1" 
		uistep = "0.001"
		min = "1.0" 
		max = "10.0" 
		init = "1.0"
	>;
	// ——————————————————DIFFUSE——————————————————

	["漫反射强度"]
	uniform mediump float diffuse_intensity
	<
		init = "1.0"
		max = "5.0"
		min = "0.0"
		uistep = "0.001"
		artist = "0"
		uigroup = "0 漫反射"
	>;

	["角色高度"]
	uniform mediump float char_height
	<
		artist = "1"
		init = "20"
		min = "0"
		uigroup = "风格化"
	>;

	["暗部颜色"]
	uniform lowp float4 dark_color
	<
		uigroup = "风格化"
		artist = "1" 
		color = "1"
		init = "(0.15,0.15,0.15,0.15)"
	>;	

	// ——————————————————自投影——————————————————
	["阴影bias"]
	uniform mediump float shadowmap_bias
	<
		uigroup = "自投影"
		init = "0.5"
		artist = "1"
	>;

	["阴影采样bias"]
	uniform mediump float shadowmap_uv_bias
	<
		uigroup = "自投影"
		init = "0.5"
		artist = "1"
	>;

	//————————————————补光——————————————————

	["补光强度"]
	uniform mediump float dir_ambient_intensity
	<
		init = "0.9"
		max = "10.0"
		min = "0.0"
		uistep = "1"
		artist = "1"
		uigroup = "补光"
	>;	

	["补光颜色"]
	uniform lowp float4 dir_ambient
	<
		artist = "1" 
		color = "1"
		init = "(0.2,0.3804,0.7765,1)"
		uigroup = "补光"
	>;	
	
	["补光位置x"]
	uniform mediump float l_pos_x
	<
		init = "-65"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "100"
		min = "-100"
	>;
	["补光位置y"]
	uniform mediump float l_pos_y
	<
		init = "14"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "100"
		min = "-100"
	>;
	["补光位置z"]
	uniform mediump float l_pos_z
	<
		init = "100"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "100"
		min = "-100"
	>;
	["补光遮挡"]
	uniform mediump float dir_ambient_occlusion
	<
		init = "0"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "1"
		min = "0"
	>;

	["自发光bloom强度"]
	uniform mediump float emissive_bloom
	<
		uigroup = "bloom"
		// uistep = "0.001"
		init = "75.0"
		// max = "5.0"
		// min = "0.0"
		artist = "1"
	>;

	["bloom强度"]
	uniform mediump float illum_multi
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "1"
		max = "10.0"
		min = "0.0"
		artist = "1"
	>;
	["bloom阈值"]
	uniform lowp float bloom_threshold
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "0.035"
		max = "5.0"
		min = "-1.0"
		artist = "1"
	>;
	["bloom范围"]
	uniform lowp float bloom_range
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "0.4"
		max = "1.0"
		min = "-1.0"
		artist = "1"
	>;


	// —————————————————— FRESNEL ——————————————————


	["覆盖范围"]
	uniform lowp float rim_power
	<
		uigroup = "3 rim"
		init = "0"
		uistep = "0.001"
		artist = "1"
		max = "1.0"
		min = "-1.0"
	>;
	// ["强度"]
	// uniform mediump float rim_multi
	// <
	// 	uigroup = "3 rim"
	// 	init = "2"
	// 	uistep = "0.001"
	// 	artist = "1"	
	// 	max = "5.0"
	// 	min = "1.0"
	// >;

	// ["轮廓颜色"]
	// uniform lowp float4 rim_color1
	// <
	// 	uigroup = "3 rim"
	// 	init = "(0.0627,0.3686,0.8,1)"
	// 	color = "1"
	// 	artist = "1"
	// >;

	// ["内部颜色"]
	// uniform lowp float4 rim_color2
	// <
	// 	uigroup = "3 rim"
	// 	init = "1"
	// 	artist = "1"
	// 	color ="1"
	// 	init = "(0.2902,0.651,0.9529,1)"
	// >;	

	["内部透明度"]
	uniform lowp float inner_alpha
	<
		uigroup = "3 rim"
		uistep = "0.001"
		init = "1"
		max = "1.0"
		min = "-0.5"
		artist = "1"
	>;

	// ["rim 开始"]
	// uniform lowp float u_rim_start
	// <
	// 	uigroup = "3 rim"
	// 	uistep = "0.001"
	// 	init = "0"
	// 	max = "1.0"
	// 	min = "0.0"
	// 	artist = "1"
	// >;

	// ["rim 结束"]
	// uniform lowp float u_rim_end
	// <
	// 	uigroup = "3 rim"
	// 	uistep = "0.001"
	// 	init = "0.0"
	// 	max = "1.0"
	// 	min = "0.0"
	// 	artist = "1"
	// >;

	["ChangeAmount"]
	uniform lowp float ChangeAmount
	<
		uigroup = "3 rim"
		uistep = "0.001"
		init = "0"
		max = "1.0"
		min = "0.0"
		artist = "1"
	>;
	// —————————————————— 网格闪烁消融 ——————————————————
	
	["底部/顶部追加高度(消除边缘跳跃)"]
	uniform mediump float burn_add_height
	<
		init = "0.0"
		artist = "1"
		uistep = "0.01"
		max = "30.0"
		min = "0.0"
		uigroup = "网格闪烁消融"
	>;

	["网格密度"]
	uniform mediump float burn_web_density
	<
		init = "2.0"
		artist = "1"
		uistep = "0.001"
		max = "10.0"
		min = "0.2"
		uigroup = "网格闪烁消融"
	>;

	["线条宽度（网格间隙）"]
	uniform mediump float burn_line_width
	<
		init = "0.1"
		artist = "1"
		uistep = "0.001"
		max = "1.0"
		min = "0.0"
		uigroup = "网格闪烁消融"
	>;

	["消融扩散范围"]
	uniform lowp float burn_spread
	<
		init = "0.1"
		artist = "1"
		uistep = "0.001"
		max = "1.0"
		min = "0.0"
		uigroup = "网格闪烁消融"
	>;

	["网格噪声强度"]
	uniform lowp float burn_noise_intensity
	<
		init = "0.1"
		artist = "1"
		uistep = "0.001"
		max = "0.3"
		min = "0.0"
		uigroup = "网格闪烁消融"
	>;
	//特效
	["边缘光Bias"]
	uniform lowp float _Rim_Bias
	<
		artist = "1"
		uigroup = "0.边缘光"
		init = "0.0"
	>;
	["边缘光强度"]
	uniform lowp float _Rim_Scale
	<
		artist = "1"
		uigroup = "0.边缘光"
		init = "0.58"
		uistep = "0.01"
		max = "2.0"
		min = "-2.0"
	>;
	["边缘光对比度"]
	uniform lowp float _Rim_Power
	<
		artist = "1"
		uigroup = "0.边缘光"
		init = "1.0"
	>;

	["扰动Tilling"]
	uniform mediump float2 _Distort_Tilling
	<
		artist = "1"
		uigroup = "1.扰动"
		init = "2.0,1.5"
	>;
	["扰动Speed"]
	uniform mediump float2 _Distort_Speed
	<
		artist = "1"
		uigroup = "1.扰动"
		init = "0.0,-0.1"
	>;
	
	["流光纹理扰动程度"]
	uniform mediump float _Emiss_ParallaxHeight
	<
		artist = "1"
		uigroup = "2.流光"
		init = "300.0"
	>;
	["流光Tilling"]
	uniform mediump float2 _Emiss_Tilling
	<
		artist = "1"
		uigroup = "2.流光"
		init = "5.0,5.0"
	>;
	["流光Speed"]
	uniform mediump float2 _Emiss_Speed
	<
		artist = "1"
		uigroup = "2.流光"
		init = "0.3,0.3"
	>;
	["流光Fresnel Bias"]
	uniform lowp float _Emiss_Fresnel_Bias
	<
		artist = "1"
		uigroup = "2.流光"
		init = "0.0"
	>;
	["流光Fresnel强度"]
	uniform lowp float _Emiss_Fresnel_Scale
	<
		artist = "1"
		uigroup = "2.流光"
		init = "1.0"
	>;
	["流光Fresnel对比度"]
	uniform lowp float _Emiss_Fresnel_Power
	<
		artist = "1"
		uigroup = "2.流光"
		init = "1.5"
	>;

	["Ramp遮罩Tilling"]
	uniform mediump float2 _Mask_Tilling
	<
		artist = "1"
		uigroup = "3.遮罩"
		init = "9.0,12.5"
	>;
	["Ramp遮罩Speed"]
	uniform mediump float2 _Mask_Speed
	<
		artist = "1"
		uigroup = "3.遮罩"
		init = "0.0,-0.3"
	>;
	["Ramp遮罩扰动程度"]
	uniform mediump float _Mask_ParallaxHeight
	<
		artist = "1"
		uigroup = "3.遮罩"
		init = "-50.0"
	>;
	["Ramp遮罩强度"]
	uniform mediump float _MaskIntensity
	<
		artist = "1"
		uigroup = "3.遮罩"
		init = "1.0"
	>;

	["_FinalIntensity"]
	uniform lowp float _FinalIntensity
	<
		artist = "1"
		init = "1.35"
	>;

	["poisson uniform values"]
	uniform lowp float4[8] poissonDisk
	<
		init = "(0.0, 0.0, -0.94201624, -0.39906216, 0.94558609, -0.76890725, -0.094184101, -0.92938870, 0.34495938, 0.29387760, -0.91588581, 0.45771432, -0.81544232, -0.87912464, -0.38277543, 0.27676845, 0.97484398, 0.75648379, 0.44323325, -0.97511554, 0.53742981, -0.47373420, -0.26496911, -0.41893023, 0.79197514, 0.19090188, -0.24188840, 0.99706507, -0.81409955, 0.91437590, 0.19984126, 0.78641367)"
	>;

	["晶格bloom强度"]
	uniform mediump float webBloom
	<
		uigroup = "bloom"
		init = "1.0"
		artist = "1"
	>;
	
	uniform lowp float u_tonemapping_factor
	<
		min = "0.0"
		max = "1.0"
		init = "1.0"
		artist = "1"
	>;
	["蒙皮数据"]
	uniform highp float4[BONE_VEC_COUNT] u_bone_vec: BONE_VEC;

	// attribute变量
	attribute float4 texcoord0 : TEXTURE0;
	attribute float4 texcoord1 : TEXTURE1;
	attribute float4 a_color : DIFFUSE;
	attribute float4 a_position : POSITION;
	attribute float4 a_normal : NORMAL;
	attribute float4 a_tangent : TANGENT;
	["gpu蒙皮的索引"]
	attribute float4 a_blendindices: BLENDINDICES;
	["gpu蒙皮的权重"]
	attribute float4 a_blendweight: BLENDWEIGHT;

	
	// varying变量
	varying mediump float4 UV0;
	varying mediump float2 UV1; // 贴花的uv
	// varying mediump float v_char_height; 
	varying mediump float4 PosWorld;
	varying lowp float3 v_TangentWorld;
	varying lowp float3 v_BinormalWorld;
	varying lowp float3 v_NormalWorld;
	varying lowp float3 GiLightColor;
	varying highp float4 PosLightProj;
	varying highp float4 v_dissolve_uv;
	
	varying mediump float4 v_light_proj_pos;
	uniform mediump float4 u_shadowmap_info: SHADOW_INFO;
	uniform mediump float2 shadow_bias_factor
	<
		// ios(0.002, 0.0002)
		// 其他(0.004, 0.001)
		init = "(0.004, 0.001)"
	>;

	// uniform变量
	uniform highp float4x4 wvp : WORLD_VIEW_PROJECTION;
	uniform highp float4x4 world : WORLD;
	
	uniform mediump float FrameTime: FRAME_TIME;
	
	uniform mediump float4x4 texTrans0 : TEXTURE_TRANS0;
	uniform mediump float4x4 lightmapTrans : TEXTURE_TRANS1;
	
	uniform mediump float4 LightMapScale : LIGHT_MAP_SCALE;
	uniform highp float4x4 lv : LIGHT_VIEW;
	
	uniform mediump float4[LIGHT_ATTR_ITEM_NUM] DirLightAttr : DIR_LIGHT_ATTR;
	uniform mediump float4[LIGHT_ATTR_ITEM_NUM] ShadowLightAttr : SHADOW_LIGHT_ATTR;
	uniform mediump float4[LIGHT_ATTR_ITEM_NUM] PointLightAttrs : POINT_LIGHT_ATTR;

	uniform mediump float4 camera_pos : CAMERA_POSITION;
	
	uniform mediump float4x4 inverse_view : INV_VIEW;
	
	uniform mediump float4x4 view_mat : VIEW;
	
	uniform highp float4x4 lvp : LIGHT_VIEW_PROJ;
	
	uniform mediump float4 ZBufferParams: ZBUFFER_PARAMS;

	uniform float alphaRef : ALPHA_REF
	<
		init = "0.0"
	>;
	uniform float AlphaMtl : ALPHA_MTL
	<
		init = "1.0"
	>;

	// uniform mediump float4x4 envSHR
	// <
	//	 init = "(0.014678, -0.012044, -0.040482, -0.029212, -0.012044, -0.014678, 0.022421, 0.014336, -0.040482, 0.022421, 0.136843, 0.068306, -0.029212, 0.014336, 0.068306, 0.175862)"
	// >;
	// uniform mediump float4x4 envSHG
	// <
	//	 init = "(0.008648, -0.017736, -0.028054, -0.027763, -0.017736, -0.008648, 0.025042, 0.036537, -0.028054, 0.025042, 0.103563, 0.055276, -0.027763, 0.036537, 0.055276, 0.206319)"
	// >;
	// uniform mediump float4x4 envSHB
	// <
	//	 init = "(-0.002828, -0.022570, -0.018314, -0.026353, -0.022570, 0.002828, 0.025450, 0.066163, -0.018314, 0.025450, 0.069045, 0.040577, -0.026353, 0.066163, 0.040577, 0.261003)"
	// >;

	
	uniform lowp float4 FogColor : FOG_COLOR;
	uniform mediump float4 FogInfo : FOG_INFO;
	uniform highp float4x4 proj : PROJECTION;
	
	vertex
	{
		["是否开启GPU蒙皮"]
		macro(GPU_SKIN_ENABLE) out (highp float4 vec, highp float4 nor)
		{
			branch("1")
			{
				//function get_skin_pos_normal(a_blendweight, uint4(a_blendindices), a_position, a_normal, u_bone_vec) out (highp float4 pos1, highp float4 nor1);
				// out(vec, pos1);
				// out(nor, nor1);
				out(vec, a_position);
				out(nor, a_normal);
			}
			branch("0") 
			{
				out(vec, a_position);
				out(nor, a_normal);
			}
		};

		
		highp float4 position = vec;
		highp float4 normal = nor;
		

		//mediump float4 uvxyz = float4(texcoord0.xy, 1.0f, 0.0f) * texTrans0;

		// 计算顶点位置
		highp float4 screen_pos = float4(transform(position, wvp));
		vertex(screen_pos);
		
		highp float4 pos_world = transform(position, world);
		
		float3x3 worldMat = float3x3(world);
		
		highp float3 world_normal = float3(normalize(transform(normal.xyz, worldMat)));
		
		out(UV0, float4(texcoord0.xy, 0.0f, 0.0f));

		// 贴花UV
		// macro(DECAL_ENABLE){
		// 	branch("1"){
		// 		out(UV1, a_color.xy);
		// 	}
		// 	branch("0"){

		// 	}
		// };

		
		out(PosWorld, float4(pos_world.xyz, 1.0f));
		
		out(v_NormalWorld, world_normal);

		mediump float3 tangentWorld = normalize(float3(normalize(a_tangent.xyz * worldMat)));
		out(v_TangentWorld, tangentWorld);
		highp float tan_coef = 1.0f - 2.0f * step(1.5f, length(a_tangent.xyz));
		mediump float3 binormal_world = normalize(cross(tangentWorld, world_normal) * tan_coef);
		out(v_BinormalWorld,binormal_world);

		macro(SHADOW_MAP_ENABLE)
		{
			branch("1")
			{				
				macro(POINT_SHADOW_MAP_ENABLE)
				{
					branch("1")
					{
						out(v_light_proj_pos, pos_world);
					
					}
					branch("0")
					{
						mediump float4 shadow_light_attr_3 = ShadowLightAttr[3];
						function calc_shadow_info(
						pos_world,
						world_normal,
						lvp,
						shadow_light_attr_3.xyz)
					out(
						highp float2 uv_out,
						highp float  depth_out,
						highp float  factor_out
					);

						out(v_light_proj_pos, float4(uv_out, depth_out, factor_out));
											
					}
				
				};
			}
			branch("0")
			{

			}				
		};
	}



	pixel
	{
		
		// —————————————————— 法线 ——————————————————	
		lowp float3 vertex_normal_world = v_NormalWorld;
		macro(XNORMAL_MAP_ENABLE) out(lowp float3 normalWorld, lowp float occlusion) {
			branch("1") {
				lowp float4 norm = sample(SamplerNormalMap, UV0.xy);
				
				lowp float2 normOff = norm.xy * 2.0f - float2(1.0f);
				// lowp float3 normalWorld_inner = normalize(v_TangentWorld * normOff.x + v_BinormalWorld * normOff.y + vertex_normal_world);
				lowp float3 normalWorld_inner = normalize(v_TangentWorld * normOff.x + v_BinormalWorld * normOff.y + vertex_normal_world);
				out(normalWorld, normalWorld_inner);
				out(occlusion, norm.z);
			}	
			branch("0") {
				out(normalWorld, vertex_normal_world);
				out(occlusion, 1.0f);
			}
		};		

		// —————————————————— 自投影(shadow_fac计算) ——————————————————	
		macro(SHADOW_MAP_ENABLE) out (mediump float shadow_fac_)
		{
			branch("1") {
				macro(POINT_SHADOW_MAP_ENABLE) out(mediump float fac)
				{
					branch("0")
					{
						function calc_shadowmap_factor1(SMAP, v_light_proj_pos, u_shadowmap_info, shadow_bias_factor * shadowmap_bias, shadowmap_uv_bias)
							out(lowp float factor);
							// 自投影的浓度为0.8
						// out(shadow_fac_, lerp(1.0f,factor,0.8f));
						out(fac, factor);
					}
					branch("1")
					{	
						mediump float4 shadow_light_attr_3 = ShadowLightAttr[3];
						function calc_point_shadow_info(
							v_light_proj_pos,
							vertex_normal_world,
							lvp,
							lv,
							shadow_light_attr_3.xyz)
						out(
							highp float2 uv_out,
							highp float  depth_out,
							highp float  factor_out
						);
						float4 cal_pos = float4(uv_out, depth_out, factor_out);
						function calc_shadowmap_factor(SMAP, cal_pos, u_shadowmap_info, shadow_bias_factor)
							out(lowp float factor);
							// 自投影的浓度为0.8
						// out(shadow_fac_, lerp(1.0f,factor,0.8f));
						out(fac, factor);							
					}
				};
				out(shadow_fac_, fac);

			}
			branch("0") {
				out(shadow_fac_, 1.0f);
			}
		};


		// ——————————————————★★★ 基础设置 ★★★——————————————————	
		// 采样贴图
		lowp float4 diffuse_map_color = sample(Tex0, UV0.xy);

		// macro(DIFFUSE_ALPHA_SEPARATE) out(lowp float4 diffuse_map_color)
		// {
		// 	branch("1")
		// 	{
		// 		lowp float4 separateColor = sample(sepAlphaTex, UV0.xy);
		// 		out(diffuse_map_color, float4(diffuse_map_color_.xyz, separateColor.w));
		// 	}
		// 	branch("0") {
		// 		out(diffuse_map_color, diffuse_map_color_);
		// 	}
		// };

		lowp float4 pbr_map = sample(SamplerRough, UV0.xy);
		lowp float metal = saturate(pbr_map.r + metal_multi);
		lowp float rough = saturate(pbr_map.b + rough_multi);

		// ——————————————————★★★ 光照设置 ★★★——————————————————
		macro(POINT_LIGHT_ENABLE) out(mediump float3 gi_color, mediump float3 light_dir) {
			branch ("1") {
				// 点光颜色
				lowp float4 light_color = ShadowLightAttr[1];
				mediump float4 dir_vector = ShadowLightAttr[3];
				lowp float light_range = dir_vector.w;
				mediump float3 light_dir_ = PosWorld.xyz - dir_vector.xyz; 
				out(light_dir, normalize(light_dir_)); 
				lowp float dist = length(light_dir_);
				lowp float point_light_fac = clamp((light_range - dist) / light_range, 0.0f, 1.0f);
				out(gi_color, lerp(dark_color.rgb, light_color.rgb, float3(point_light_fac * point_light_fac)));
			}
			branch ("0"){
				//平行光颜色
				lowp float4 light_color = ShadowLightAttr[1]; 
				out(gi_color, light_color.xyz); 

				// 光源方向
				mediump float4 light_dir_ = ShadowLightAttr[3];
				out(light_dir, normalize(light_dir_.xyz));
				//mediump float3 light_dir = u_light_scale * normalize(float3(u_light_pos_x, u_light_pos_y, u_light_pos_z));
			}

		};

		lowp float3 light_dir_ndl = normalize(float3(light_dir.x, light_dir.y, light_dir.z));

		// 视线方向
		lowp float3 view_dir = normalize(camera_pos.xyz - PosWorld.xyz);

		// ——————————————————★★★ 常用参数 ★★★——————————————————

		lowp float roughness = rough;	
				
		lowp float non_metal = 1.0f - metal;

		lowp float3 H = normalize(view_dir - light_dir_ndl);
		mediump float VoH = dot(view_dir, H);
		mediump float VoL = dot(view_dir, -light_dir_ndl);
		mediump float NoV = dot(view_dir, normalWorld);
		mediump float VdotH = saturate(VoH);
		mediump float NoH = dot(normalWorld, H);
		mediump float NdotH = saturate(NoH);
		mediump float NdotV = saturate(NoV);

		// NdotL
		mediump float NoL = dot(normalWorld, -light_dir_ndl);

		// 计算OrenNayarLighting因子
		lowp float ggx_rough = lerp(0.04f, 1.0f, rough);

		macro(ORENNAYAR_ENABLE) out(lowp float orennayar_factor) {
			branch ("0") {
				out(orennayar_factor,1.0f);
			}
			branch ("1") {
				float a = ggx_rough * ggx_rough;
				float s = a;// / ( 1.29 + 0.5 * a );
				float s2 = s * s;
				float VoL1 = 2.0f * VoH * VoH - 1.0f;		// double angle identity
				float Cosri = VoL1 - NoV * NoL;
				float C1 = 1.0f - 0.5f * s2 / (s2 + 0.33f);
				condition(Cosri) out(float C3) {
					branch ("%0% >= 0.0") {
						out(C3, min(1.0f,NoL/max(0.01f, NoV)));
					}
					default {
						out(C3, 1.0f);
					}
				};
				float C2 = 0.45f * s2 / (s2 + 0.09f) * Cosri * C3;
				out(orennayar_factor, clamp(( C1 + C2 ) * ( 1.0f + ggx_rough * 0.5f), 0.0f, 5.0f));
			}
		};
		

		["计算暗部补充光线"]
		// mediump float3 fix_normal = normalize(vertex_normal_world + 0.1f * view_dir);
		mediump float3 fix_normal = normalize(normalWorld + 0.1f * view_dir);
		mediump float fix_nol = saturate(dot(fix_normal, light_dir_ndl));
		mediump float light_view_dir = transform(light_dir_ndl, float3x3(view_mat)).b;
		mediump float ambient_fix = 0.5f * fix_nol *fix_nol * (0.2f + saturate(light_view_dir + 0.5f));
		
		// mediump float NdotL_spec = min(saturate(NoL),shadow_fac_);// 此为specular的nol
		mediump float NdotL_spec = min(saturate(NoL),shadow_fac_);
		lowp float NdotL_diff = NdotL_spec * orennayar_factor;

		// ——————————————————★★★ DIFFUSE ★★★—————————————————— 
		mediump float3 diffuse_color = diffuse_map_color.xyz * diffuse_map_color.xyz * diffuse_intensity; 
				

		// 金属度计算过后的漫反射光强
		mediump float3 non_metal_diffuse = non_metal * diffuse_color; 

		// 方向环境光 		
		macro (DIR_AMBIENT_ENABLE) out (mediump float3 dir_ambient_color)
		{
			branch("1"){
				mediump float3x3 inverse_view_mat = float3x3(inverse_view);
				//mediump float dir_ambient_fac = clamp(-normalWorld.y, 0.0f, 1.0f);
				mediump float3 ambient_dir = normalize(float3(l_pos_x, l_pos_y, l_pos_z) * inverse_view_mat);
				lowp float dir_ambient_fac_ = clamp(dot(normalWorld, ambient_dir), 0.0f, 1.0f);
				lowp float dir_ambient_oc = lerp(1.0f,NdotL_spec,dir_ambient_occlusion);
				out(dir_ambient_color, dir_ambient.rgb * dir_ambient_fac_ * dir_ambient_intensity * (0.5f * non_metal + 0.5f) * dir_ambient_oc);
			}
			branch("0"){
				out(dir_ambient_color, float3(0.0f));	

			}
		};

		// AO
		macro(ENVIR_ENABLE) out(lowp float3 AO_color) {
			branch ("1") {
				lowp float _AO_adjust = lerp(1.0f, occlusion ,NdotV);
				out(AO_color, float3(lerp(_AO_adjust, occlusion,2.0f * saturate(0.5f - occlusion))));
			}
			branch ("0") {
				out(AO_color, float3(1.0f));
			}
		};


		macro(ENVIR_ENABLE) out(mediump float3 R_rotated) {
			branch ("1") {
				mediump float3 R = reflect(-view_dir, normalWorld);
				out(R_rotated, R);
			}
			branch("0"){
				out(R_rotated, float3(0.0f));
			}
		};

		// 增加半球光照
		// lowp float3 sky_light = saturate(normalWorld.g  + skylight_smooth) * sky_light_color.rgb * sky_light_intensity * saturate(lerp(1.0f,PosWorld.y / 20.0f, sky_light_height));
		// 增加高度渐变以及垂直范围的变化
		// lowp float3 sky_light = saturate(normalWorld.g  + 0.35f) * sky_light_color.rgb * sky_light_intensity * saturate(lerp(1.0f,PosWorld.y / 20.0f, 0.31f));
		lowp float3 sky_light = float3(0.0f);

		// SH
		// function calc_env_sh( envSHR, envSHG, envSHB, float3(normal_view.x, normal_view.y,-normal_view.z)) out (mediump float3 ibl_diffuse);
		function calc_env_sh( envSHR, envSHG, envSHB, normalWorld) out (mediump float3 ibl_diffuse1);
		mediump float3 ibl_diffuse = ibl_diffuse1 * 3.0f; //环境光的sh与高光比为3:130;

		// 环境光调整色计算
		// lowp float3 env_factor_color = lerp(env_shadow_factor.rgb ,float3(1.0f), float3(NdotL_diff));
		lowp float3 env_factor_color = env_shadow_factor.rgb * (1.0f + ambient_fix) + NdotL_diff * gi_color * 0.5f; 
		// lowp float3 env_factor_color = env_shadow_factor.rgb ; 

		// Final Diffuse
		// 固有色 * (主光源 + 环境光 + 天光) + 补光
		mediump float3 final_diffuse = non_metal_diffuse * ((NdotL_diff + ambient_fix) * gi_color  + ibl_diffuse * (sky_light + env_factor_color * envir_brightness)) + dir_ambient_color;

		// —————————————————— ★★★ SPECULAR ★★★—————————————————— 
		mediump float3 spec_color = lerp(diffuse_color.xyz, float3(0.04f), float3(non_metal)); 

		// F
		function F_Schlick(spec_color,VdotH) out (lowp float3 F);	
		// D 
		function D_GGX(ggx_rough, NdotH) out(lowp float D);
		
		// G
		function G_Schlick_Disney(ggx_rough, NdotV, NdotL_spec) out (lowp float G);
		// specular BRDF
		//mediump float3 brdf =  D * spec_color * spec_intensity;

		mediump float3 brdf =  D * F * G  ;

		// Final Specular

		mediump float3 final_specular = brdf * NdotL_spec * gi_color;


		// ——————————————————★★★ IBL ★★★—————————————————— 
		macro(ENVIR_ENABLE) out(mediump float3 final_envir)
		{
			branch("1") {

				function GetEnvirmentColor_panorama (SamplerCubeMap, roughness, R_rotated, 130.0f) out (mediump float3 envir); // 环境光最大强度不改了，再改是猪！
				function EnvBRDFApprox(spec_color, ggx_rough, NdotV, oneminus(NdotL_spec * 0.8f) * envir_fresnel_brightness) out (mediump float3 env_brdf);
				out(final_envir,  env_brdf * envir * (sky_light + env_factor_color * envir_brightness));
			}
			branch("0") {
				out(final_envir, diffuse_color * ibl_diffuse * metal * lerp(0.6f, 1.0f, NdotH));
			}
		};

		// ——————————————————★★★ 自发光 & 半透 ★★★—————————————————— 
		lowp float arufa = diffuse_map_color.a; 
		macro(SELF_BLING) out (mediump float3 final_emissive, lowp float emissive_area){
			branch("1"){
				mediump float3 final_emissive_ = arufa * emissive_intensity * non_metal_diffuse.rgb;

				out(final_emissive, final_emissive_);
				out(emissive_area, arufa);
				// out(diffuse_alpha, _alpha);
			}
			branch("0"){
				out(final_emissive, float3(0.0f));
				out(emissive_area, 0.0f);
				// out(diffuse_alpha, diffuse_map_color.a);
			}
		};

		lowp float diffuse_alpha = 1.0f; //取消半透明功能

		/////////////////////////////////////////////////////////////////
		// ——————————————————★★★ 最终光照计算 ★★★——————————————————
		/////////////////////////////////////////////////////////////////
		// pbr的最终计算结果 = (漫反射 + 动态光反射 + 环境反射）* 主光颜色 * AO  （IBL也要受主光颜色影响，这样不会出现与主光色调不一致的情况）

		mediump float3 combine_spec = final_specular + final_envir ;
		mediump float3 pbr_light_color =  (final_diffuse + combine_spec) * AO_color;
		mediump float3 result_diff_raw =  pbr_light_color + final_emissive;

		// —————————————————— 边缘光 ———————————————————
		//lowp float rim_ = saturate(1.0f - NdotV);
		
		macro(WEB_SHINING_ENABLE) out(mediump float3 fresnel_combine) 
		{
			branch ("0") 
			{
				out(fresnel_combine,result_diff_raw);
			}
			branch ("1") 
			{
				["网格闪烁消融效果"]
				function web_shining
				(
					burn_max_height, //角色身高
					burn_color01,   //闪烁颜色(较暗)
					burn_color02,	  //闪烁颜色(较亮)
					burn_style,		 //消融方向控制
					PosWorld.xyz,	   //当前像素位置
					(world[3]).xyz,   //角色位置 
					ChangeAmount,	   //消融量
					result_diff_raw   //当前颜色 
				)
				out(lowp float3 final_shining_color, lowp float alphatest01, lowp float alphatest02);

				alphatest("<")(alphatest01, alphatest02);
				out(fresnel_combine,final_shining_color.rgb);
			}
		};

		lowp float emissive_bloom1 = emissive_bloom;
		lowp float tmp = bloom_range + illum_multi; // 只是为了美术同学可以调整

		float web_light = dot(abs(fresnel_combine - result_diff_raw), float3(0.3f, 0.59f, 0.11f)); // 网格效果辉光
				
		function unreal_tonemapping(fresnel_combine, ToneFactor, 0.88f, 0.55f, 0.26f, 0.00f, 0.04f) out(float3 ToneColor_aces);
		
		function Luminance(combine_spec) out (mediump float color_illum);
		lowp float4 encode_color_1 = float4(ToneColor_aces, 1.0f);

		mediump float2 worlduv = PosWorld.xy * float2(0.025f);
		mediump float rim_factor = _Rim_Bias + _Rim_Scale * pow(1.0f - NdotV,_Rim_Power);
		mediump float emiss_fresnel = _Emiss_Fresnel_Bias + _Emiss_Fresnel_Scale * pow(1.0f - NdotV, _Emiss_Fresnel_Power);
		//lowp float noise = sample(_DistortMap,_Distort_Tilling * worlduv + float2(FrameTime) * _Distort_Speed).r;
		lowp float noise = sample(_EmissTex,_Distort_Tilling * worlduv + float2(FrameTime) * _Distort_Speed).g;

		float3x3 tangentTransform = transpose(float3x3(v_TangentWorld,v_BinormalWorld,v_NormalWorld));
		float3 view_dir_tangent = normalize(transform(view_dir,tangentTransform));
		function ParallaxOffset(noise,_Emiss_ParallaxHeight * 0.01f,view_dir_tangent) out(float2 uvoffset);
		lowp float emiss_value = sample(_EmissTex, _Emiss_Tilling * worlduv + float2(FrameTime) * _Emiss_Speed + float2(sin(FrameTime)) * float2(0.1f,0.0f) + uvoffset).r;
		mediump float ramp_u = rim_factor + emiss_fresnel * emiss_value;
		//lowp float rampmask_value = sample(_RampMask, uvoffset + _Mask_Tilling * worlduv + float2(FrameTime) * _Mask_Speed).r;
		lowp float rampmask_value = sample(_EmissTex, uvoffset + _Mask_Tilling * worlduv + float2(FrameTime) * _Mask_Speed).b;
		lowp float ramp_mask =  noise - rampmask_value * _MaskIntensity;
		lowp float4 ramp_color = lerp(sample(_Ramp, float2(ramp_u,0.25f)), sample(_Ramp, float2(ramp_u,0.75f)),float4(ramp_mask));
		lowp float3 vfx_color = ramp_color.xyz * float3(_FinalIntensity);
		lowp float3 res_color = encode_color_1.rgb + vfx_color;

		lowp float alphchannel = saturate((emiss_fresnel * emiss_value - bloom_threshold)) * emissive_bloom1 + web_light * webBloom;
		lowp float4 combined_color = float4(float3(res_color),alphchannel);

		// —————————————————— diffuse check ———————————————————
		macro(DIFFUSE_CHECK) out(mediump float4 diffuse_check){
			branch("1"){
				out(diffuse_check, diffuse_map_color);
			}
			branch("0"){
				out(diffuse_check, float4(combined_color));
			}
		};
		
		macro(ROUGHNESS_CHECK) out(lowp float4 final_color1)
		{
			branch ("0") {
				out(final_color1, diffuse_check);
			}
			branch ("1") {
				out(final_color1, float4(ggx_rough,ggx_rough,ggx_rough,1.0f));
			}
		};

		macro(METAL_CHECK) out(lowp float4 final_color2)
		{
			branch ("0") {
				out(final_color2, final_color1);
			}
			branch ("1") {
				out(final_color2, float4(metal,metal,metal,1.0f));
			}
		};

		macro(AO_CHECK) out(lowp float4 final_color3)
		{
			branch ("0") {
				out(final_color3, final_color2);
			}
			branch ("1") {
				out(final_color3, float4(occlusion));
			}
		};
		pixel(float4(final_color3));
	}
}

