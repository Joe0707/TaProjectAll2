["VBR shader for DM106"]
pipeline
<
	version = "1.1.2" 
	author = "liuyu/xuzhuoliang/mengqingyu" 
	category = "common" 
	company = "netease"
	merge = "1"
>
{
	include(shader\\including\\envir_uniform.spzs)
	include(shader\\including\\pbr_include_function.spzs)

	["是否启用场景转置"]
	eitheror TRANS_ENABLE;
	macro(TRANS_ENABLE)
	<
		customizable = "1"
		default = "0"
	>;

	["检查AO"]
	eitheror AO_CHECK;
	macro(AO_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["检查diffuse"]
	eitheror DIFFUSE_CHECK;
	macro(DIFFUSE_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["检查金属度"]
	eitheror METAL_CHECK;
	macro(METAL_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["检查粗糙度"]
	eitheror ROUGHNESS_CHECK;
	macro(ROUGHNESS_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["开启环境反射(ENVIR_ENABLE)"]
	eitheror ENVIR_ENABLE;

	["开启法线贴图(XNORMAL_MAP_ENABLE)"]
	eitheror XNORMAL_MAP_ENABLE;
	
	["补光"]
	eitheror DIR_AMBIENT_ENABLE;

	["开启自发光"]
	eitheror SELF_BLING;

	["接受点光"]
	eitheror POINT_LIGHT_ENABLE;

	// ["开启羽毛反射"]
	// eitheror FUR_COLOR_ENABLE;

	["是否开启Oren-nayar因子"]
	eitheror ORENNAYAR_ENABLE;
	macro(ORENNAYAR_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

	["开启网格闪烁消融"]
	eitheror WEB_SHINING_ENABLE;
	macro(WEB_SHINING_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

/*
	["使用溶解效果"]
	eitheror DISSOLVE_ENABLE;
	macro(DISSOLVE_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	["使用柔边溶解"]
	eitheror SOFTEDGE_ENABLE;
	macro(SOFTEDGE_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;
	*/

	macro(SHADOW_MAP_ENABLE)
	<
		default = "1"
	>;
	
	macro(POINT_SHADOW_MAP_ENABLE)
	<
		default = "0"
	>;
	
	macro(XNORMAL_MAP_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

	macro(ENVIR_ENABLE)
	<
		default = "1"
		customizable = "1"
	>;

	macro(DIR_AMBIENT_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	macro(SELF_BLING)
	<
		default = "0"
		customizable = "1"
	>;

	macro(POINT_LIGHT_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	["开启流动效果"]
	eitheror FLOW_CHECK;
	macro(FLOW_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	["使用2U控制流动与噪声"]
	eitheror UV002_ENABLE;
	macro(UV002_ENABLE)
	<
		default = "0"
		customizable = "1"
	>;

	["开启法线噪声"]
	eitheror NORMAL_NOISE_CHECK;
	macro(NORMAL_NOISE_CHECK)
	<
		default = "0"
		customizable = "1"
	>;

	// macro(FUR_COLOR_ENABLE)
	// <
	// 	default = "0"
	// 	customizable = "1"
	// >;

	// 贴图
	["漫反射贴图"]
	uniform sampler Tex0 : DIFFUSE
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;

	["变色贴图"]
	uniform sampler changecolor_mask
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;	

	["SSS贴图"]
	uniform sampler sssLUT
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;
	
	["金属度/sss/光滑度"]
	uniform sampler SamplerRough
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;

	["法线贴图"]
	uniform sampler SamplerNormalMap
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;

	["各向异性贴图（RG方向，B区域）"]
	uniform sampler SamplerAniso
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;

	["SSS Ramp贴图"]
	uniform sampler sss_ramp
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		addressv = "clamp"
		addressu = "clamp"
		lodbias = "-1.0"
		path = "hero\\1001_guhuoniao\\ssslut.png"
	>;

	["溶解贴图"]
	uniform sampler u_tex_dissolve
	<
		artist = "1"
		min = "linear"
		mag = "linear"
		path = "fx\\texture\\effect\\dissolve.png"
	>;

	["KelemenLUT贴图"]
	uniform sampler KelemenLUT
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		addressv = "clamp"
		addressu = "clamp"
		lodbias = "-1.0"
		path = "hero\\1001_guhuoniao\\kelemenlut.png"
	>;

	["阴影贴图"]
	uniform highp samplershadow SMAP : SHADOW
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		addressu = "clamp" 
		addressv = "clamp" 
	>;

	["分开的alpha贴图"]
	uniform sampler sepAlphaTex: DIFFUSE_ALPHA
	<
	>;

	["FlowMap(RG)/沙子区域(B)"]
	uniform sampler flowmap
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;

	/*["原法线FlowMap(RG)"]
	uniform sampler flowmap_B
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"
		lodbias = "-1.0"
	>;*/

	["噪声贴图(RG)"]
	uniform sampler SamplerNormalMap_Add
	<
		artist = "1"
		mag = "linear"
		mip = "linear"
		min = "linear"

		path = "common\\textures\\noise.png"
	>;
	
	// —————————————————— PBR调整 ——————————————————
	["手动金属度"]
	uniform lowp float manul_metal
	<
		uigroup = "1 PBR"
		uistep = "1"
		artist = "1" 
		color = "1"
		min = "0.0"
		max = "1.0"
	>;
	
	["手动光滑度"]
	uniform lowp float manul_rough
	<
		uigroup = "1 PBR"
		uistep = "1"
		artist = "1" 
		color = "1"
		min = "0.0"
		max = "1.0"
	>;
	["金属度缩放"]
	uniform lowp float metal_multi
	<
		uigroup = "1 PBR"
		artist = "1" 
		init = "0.0"
		uistep = "0.001"
		min = "-1.0"
		max = "1.0"
	>;
	["粗糙度缩放"]
	uniform lowp float rough_multi
	<
		uigroup = "1 PBR"
		artist = "1" 
		init = "0.0"
		uistep = "0.001"
		min = "-1.0"
		max = "1.0"
	>;
	
/*
	["非金属最低灰阶(min_metal_gray)"]
	uniform mediump float min_metal_gray
	<
		uigroup = "1 PBR"
		min = "0.0" 
		max = "10.0" 
		init = "0.003"
	>;
*/
	// ——————————————————变色—————————————————————————
	["第一层颜色A"]
	uniform lowp float4 changecolor1
	<
		artist = "1"
		init = "(1.0,1.0,1.0,1.0)"
		color = "1"
		uigroup = "变色"
	>;

	["第一层颜色B"]
	uniform lowp float4 changecolor2
	<
		artist = "1"
		init = "(0.5,1.0,1.0,1.0)"
		color = "1"
		uigroup = "变色"
	>;

	["第二层颜色A"]
	uniform lowp float4 changecolor3
	<
		artist = "1"
		init = "(1.0,1.0,1.0,1.0)"
		color = "1"
		uigroup = "变色"
	>;

	["第二层颜色B"]
	uniform lowp float4 changecolor4
	<
		artist = "1"
		init = "(0.5,1.0,1.0,1.0)"
		color = "1"
		uigroup = "变色"
	>;

	["第三层颜色A"]
	uniform lowp float4 changecolor5
	<
		artist = "1"
		init = "(1.0,1.0,1.0,1.0)"
		color = "1"
		uigroup = "变色"
	>;

	["第三层颜色B"]
	uniform lowp float4 changecolor6
	<
		artist = "1"
		init = "(0.5,1.0,1.0,1.0)"
		color = "1"
		uigroup = "变色"
	>;

	["第一层(A金属度、A粗糙度、B金属度、B粗糙度)"]
	uniform lowp float4 change_adj1
	<
		artist = "1"
		init = "(0,0,0,0)"
		max = "1.0"
		min = "-1.0"
		uigroup = "变色"
	>;

	["第二层(A金属度、A粗糙度、B金属度、B粗糙度)"]
	uniform lowp float4 change_adj2
	<
		artist = "1"
		init = "(0,0,0,0)"
		max = "1.0"
		min = "-1.0"
		uigroup = "变色"
	>;

	["第三层(A金属度、A粗糙度、B金属度、B粗糙度)"]
	uniform lowp float4 change_adj3
	<
		artist = "1"
		init = "(0,0,0,0)"
		max = "1.0"
		min = "-1.0"
		uigroup = "变色"
	>;	


	["曝光度"]
	uniform mediump float expose_bias
	<
		uigroup = "1 PBR"
		artist = "1" 
		init = "1.0"
		uigroup = "1 环境光"
	>;

	["天光颜色"]
	uniform lowp float4 sky_light_color
	<
		uigroup = "1 环境光"
		artist = "0" 
		color = "1"
		init = "(0.5569,0.7412,1,1)"
	>;

	["天光强度"]
	uniform float sky_light_intensity
	<
		uigroup = "1 环境光"
		artist = "0" 
		init = "1.0"
	>;
	
	// ——————————————————各向异性——————————————————
	
	["抖动偏移值(Anisotropic_offset)"]
	uniform lowp float noise_offset
	<
		uigroup = "各向异性"
		artist = "1" 
		uistep = "0.001"
		min = "0.0" 
		max = "1.0" 
		init = "0.2"
	>;

	["整体偏移值(Anisotropic_offset)"]
	uniform mediump float normal_offset
	<
		uigroup = "各向异性"
		artist = "1" 
		uistep = "0.001"
		min = "-5" 
		max = "5" 
		init = "-0.2"
	>;
	
	// ——————————————————自发光——————————————————
	
	["自发光强度"]
	uniform mediump float emissive_intensity
	<
		uigroup = "风格化"
		artist = "1" 
		uistep = "0.001"
		min = "1.0" 
		max = "10.0" 
		init = "1.0"
	>;
	// ——————————————————DIFFUSE——————————————————

	["漫反射强度"]
	uniform mediump float diffuse_intensity
	<
		init = "1.0"
		max = "5.0"
		min = "0.0"
		uistep = "0.001"
		artist = "0"
		uigroup = "0 漫反射"
	>;

	["角色高度"]
	uniform mediump float char_height
	<
		artist = "1"
		init = "20"
		min = "0"
		uigroup = "风格化"
	>;

	["暗部颜色"]
	uniform lowp float4 dark_color
	<
		uigroup = "风格化"
		artist = "1" 
		color = "1"
		init = "(0.15,0.15,0.15,0.15)"
	>;	

	// ——————————————————自投影——————————————————
	["阴影bias"]
	uniform mediump float shadowmap_bias
	<
		uigroup = "自投影"
		init = "0.5"
		artist = "1"
	>;

	["阴影采样bias"]
	uniform mediump float shadowmap_uv_bias
	<
		uigroup = "自投影"
		init = "0.5"
		artist = "1"
	>;

	//————————————————补光——————————————————

	["补光强度"]
	uniform mediump float dir_ambient_intensity
	<
		init = "0.9"
		max = "10.0"
		min = "0.0"
		uistep = "1"
		artist = "1"
		uigroup = "补光"
	>;	

	["补光颜色"]
	uniform lowp float4 dir_ambient
	<
		artist = "1" 
		color = "1"
		init = "(0.2,0.3804,0.7765,1)"
		uigroup = "补光"
	>;	
	
	["补光位置x"]
	uniform mediump float l_pos_x
	<
		init = "-65"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "100"
		min = "-100"
	>;
	["补光位置y"]
	uniform mediump float l_pos_y
	<
		init = "14"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "100"
		min = "-100"
	>;
	["补光位置z"]
	uniform mediump float l_pos_z
	<
		init = "100"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "100"
		min = "-100"
	>;
	["补光遮挡"]
	uniform mediump float dir_ambient_occlusion
	<
		init = "0"
		artist = "1"
		uigroup = "补光"
		uistep = "0.001"
		max = "1"
		min = "0"
	>;

	["自发光bloom强度"]
	uniform mediump float emissive_bloom
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "0.0"
		max = "5.0"
		min = "0.0"
		artist = "1"
	>;

	["bloom强度"]
	uniform mediump float illum_multi
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "1"
		max = "10.0"
		min = "0.0"
		artist = "1"
	>;
	["bloom阈值"]
	uniform lowp float bloom_threshold
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "1.0"
		max = "5.0"
		min = "-1.0"
		artist = "1"
	>;
	["bloom范围"]
	uniform lowp float bloom_range
	<
		uigroup = "bloom"
		uistep = "0.001"
		init = "0.0"
		max = "1.0"
		min = "-1.0"
		artist = "1"
	>;

	["晶格bloom强度"]
	uniform mediump float webBloom
	<
		uigroup = "bloom"
		init = "1.0"
		artist = "1"
	>;

	["ChangeAmount"]
	uniform lowp float ChangeAmount
	<
		uigroup = "3 rim"
		uistep = "0.001"
		init = "0"
		max = "1.0"
		min = "0.0"
		artist = "1"
	>;
	// —————————————————— SSS ——————————————————
	["SSS强度"]
	uniform mediump float sss_factor
	<
		init = "1.0"
		artist = "1"
		uistep = "1"
		max = "1"
		min = "0"
		uigroup = "sss"
	>;	

	["SSS中阴影强度"]
	uniform mediump float sss_shadow_factor
	<
		init = "0.5"
		artist = "1"
		uistep = "1"
		max = "1"
		min = "0"
		uigroup = "sss"
	>;

	//------------ 流动贴图 -------------
	["原法线流动速度"]
	uniform mediump float flow_speed 
	<
		init = "0.5"
		artist = "1"
		uistep = "0.001"
		max = "2.0"
		min = "-2.0"
		uigroup = "流动贴图/法线噪声"
	>;

	["原法线循环周期"]
	uniform mediump float flow_period 
	<
		init = "3.0"
		artist = "1"
		uistep = "0.001"
		max = "10.0"
		min = "0.01"
		uigroup = "流动贴图/法线噪声"
	>;

	["原法线流动部分强度"]
	uniform mediump float flow_normal_intensity 
	<
		init = "1.0"
		artist = "1"
		uistep = "0.001"
		max = "5.0"
		min = "-5.0"
		uigroup = "流动贴图/法线噪声"
	>;

	["噪声流动速度"]
	uniform mediump float flow_speed_B 
	<
		init = "0.5"
		artist = "1"
		uistep = "0.001"
		max = "2.0"
		min = "-2.0"
		uigroup = "流动贴图/法线噪声"
	>;

	["噪声循环周期"]
	uniform mediump float flow_period_B 
	<
		init = "3.0"
		artist = "1"
		uistep = "0.001"
		max = "10.0"
		min = "0.01"
		uigroup = "流动贴图/法线噪声"
	>;

	["噪声法线的附加强度"]
	uniform mediump float noise_normal_intensity 
	<
		init = "1.0"
		artist = "1"
		uistep = "0.001"
		max = "2.5"
		min = "-2.5"
		uigroup = "流动贴图/法线噪声"
	>;

	["噪声法线的下落速度"]
	uniform mediump float noise_fall_speed 
	<
		init = "1.0"
		artist = "1"
		uistep = "0.001"
		max = "1.0"
		min = "-1.0"
		uigroup = "流动贴图/法线噪声"
	>;

	/*["波纹法线的附加强度"]
	uniform mediump float wave_normal_intensity 
	<
		init = "1.0"
		artist = "1"
		uistep = "0.001"
		max = "5.0"
		min = "-5.0"
		uigroup = "流动贴图/法线噪声"
	>;*/

	["噪声密度"]
	uniform mediump float noise_density 
	<
		init = "1.0"
		artist = "1"
		uistep = "0.001"
		max = "8.0"
		min = "0.1"
		uigroup = "流动贴图/法线噪声"
	>;

	//------------ 溶解效果 -------------
	/*
	["溶解TillingX"]
	uniform lowp float u_dissolve_tillingx
	<
		artist = "1"
		init = "1.0"
		uigroup = "溶解"
	>;
	["溶解TillingY"]
	uniform lowp float u_dissolve_tillingy
	<
		artist = "1"
		init = "1.0"
		uigroup = "溶解"
	>;

	["溶解程度"]
	uniform lowp float u_dissolve_intensity
	<
		artist = "1"
		max = "1.0"
		min = "0.0"
		init = "1.0"
		uistep = "0.01"
		uigroup = "溶解"
	>;

	["溶解宽度"]
	uniform lowp float u_dissolve_edge
	<
		artist = "1"
		max = "1.0"
		min = "0.0"
		init = "0.04"
		uistep = "0.01"
		uigroup = "溶解"
	>;

	["溶解边界颜色"]
	uniform lowp float4 u_dissolve_color
	<
		color = "1"
		artist = "1"
		init = "(0.215, 0.635, 1.0, 1.0)"
		uigroup = "溶解"
	>;

	["溶解边界宽度"]
	uniform lowp float u_gloss_edge
	<
		artist = "1"
		max = "1.0"
		min = "0.0"
		init = "0.05"
		uistep = "0.01"
		uigroup = "溶解"
	>;

	["溶解边界强度"]
	uniform lowp float u_gloss_strength
	<
		artist = "1"
		max = "10.0"
		min = "1.0"
		init = "10.0"
		uistep = "1"
		uigroup = "溶解"
	>;

	["溶解：使用屏幕UV"]
	uniform lowp float u_usescreen_uv
	<
		artist = "1"
		max = "1.0"
		min = "0.0"
		init = "0.0"
		uistep = "1"
		uigroup = "溶解"
	>;
	*/

	["poisson uniform values"]
	uniform lowp float4[8] poissonDisk
	<
		init = "(0.0, 0.0, -0.94201624, -0.39906216, 0.94558609, -0.76890725, -0.094184101, -0.92938870, 0.34495938, 0.29387760, -0.91588581, 0.45771432, -0.81544232, -0.87912464, -0.38277543, 0.27676845, 0.97484398, 0.75648379, 0.44323325, -0.97511554, 0.53742981, -0.47373420, -0.26496911, -0.41893023, 0.79197514, 0.19090188, -0.24188840, 0.99706507, -0.81409955, 0.91437590, 0.19984126, 0.78641367)"
	>;
	
	uniform lowp float u_tonemapping_factor
	<
		min = "0.0"
		max = "1.0"
		init = "1.0"
		artist = "1"
	>;
	["蒙皮数据"]
	uniform highp float4[BONE_VEC_COUNT] u_bone_vec: BONE_VEC;

	// attribute变量
	attribute float4 texcoord0 : TEXTURE0;
	attribute float4 texcoord1 : TEXTURE1;
	attribute float4 a_color : DIFFUSE;
	attribute float4 a_position : POSITION;
	attribute float4 a_normal : NORMAL;
	attribute float4 a_tangent : TANGENT;
	["gpu蒙皮的索引"]
	attribute float4 a_blendindices: BLENDINDICES;
	["gpu蒙皮的权重"]
	attribute float4 a_blendweight: BLENDWEIGHT;

	
	// varying变量
	varying mediump float4 UV0;
	varying mediump float2 UV1; // 贴花的uv
	// varying mediump float v_char_height; 
	varying mediump float4 PosWorld;	
	varying mediump float4 pos_local;
	varying lowp float3 v_TangentWorld;
	varying lowp float3 v_BinormalWorld;
	varying lowp float3 v_NormalWorld;
	varying lowp float3 GiLightColor;
	varying highp float4 PosLightProj;
	varying highp float4 v_dissolve_uv;
	varying highp float2 flow_uv;
	
	varying mediump float4 v_light_proj_pos;
	uniform mediump float4 u_shadowmap_info: SHADOW_INFO;
	uniform mediump float2 shadow_bias_factor
	<
		// ios(0.002, 0.0002)
		// 其他(0.004, 0.001)
		init = "(0.004, 0.001)"
	>;

	// uniform变量
	uniform highp float4x4 wvp : WORLD_VIEW_PROJECTION;
	uniform highp float4x4 world : WORLD;
	uniform highp float4x4 wv: WORLD_VIEW;
	
	uniform mediump float FrameTime: FRAME_TIME;
	
	uniform mediump float4x4 texTrans0 : TEXTURE_TRANS0;
	uniform mediump float4x4 lightmapTrans : TEXTURE_TRANS1;
	
	uniform mediump float4 LightMapScale : LIGHT_MAP_SCALE;
	uniform highp float4x4 lv : LIGHT_VIEW;
	
	uniform mediump float4[LIGHT_ATTR_ITEM_NUM] DirLightAttr : DIR_LIGHT_ATTR;
	uniform mediump float4[LIGHT_ATTR_ITEM_NUM] ShadowLightAttr : SHADOW_LIGHT_ATTR;
	uniform mediump float4[LIGHT_ATTR_ITEM_NUM] PointLightAttrs : POINT_LIGHT_ATTR;

	uniform mediump float4 camera_pos : CAMERA_POSITION;
	
	uniform mediump float4x4 inverse_view : INV_VIEW;
	
	uniform mediump float4x4 view_mat : VIEW;
	
	uniform highp float4x4 lvp : LIGHT_VIEW_PROJ;
	
	uniform mediump float4 ZBufferParams: ZBUFFER_PARAMS;

	uniform float alphaRef : ALPHA_REF
	<
		init = "0.0"
	>;
	uniform float AlphaMtl : ALPHA_MTL
	<
		init = "1.0"
	>;

	// uniform mediump float4x4 envSHR
	// <
	//     init = "(0.014678, -0.012044, -0.040482, -0.029212, -0.012044, -0.014678, 0.022421, 0.014336, -0.040482, 0.022421, 0.136843, 0.068306, -0.029212, 0.014336, 0.068306, 0.175862)"
	// >;
	// uniform mediump float4x4 envSHG
	// <
	//     init = "(0.008648, -0.017736, -0.028054, -0.027763, -0.017736, -0.008648, 0.025042, 0.036537, -0.028054, 0.025042, 0.103563, 0.055276, -0.027763, 0.036537, 0.055276, 0.206319)"
	// >;
	// uniform mediump float4x4 envSHB
	// <
	//     init = "(-0.002828, -0.022570, -0.018314, -0.026353, -0.022570, 0.002828, 0.025450, 0.066163, -0.018314, 0.025450, 0.069045, 0.040577, -0.026353, 0.066163, 0.040577, 0.261003)"
	// >;

	
	uniform lowp float4 FogColor : FOG_COLOR;
	uniform mediump float4 FogInfo : FOG_INFO;
	uniform highp float4x4 proj : PROJECTION;

	vertex
	{
		["是否开启GPU蒙皮"]
		macro(GPU_SKIN_ENABLE) out (highp float4 vec, highp float4 nor)
		{
			branch("1")
			{
				//function get_skin_pos_normal(a_blendweight, uint4(a_blendindices), a_position, a_normal, u_bone_vec) out (highp float4 pos1, highp float4 nor1);
				// out(vec, pos1);
				// out(nor, nor1);
				out(vec, a_position);
				out(nor, a_normal);
			}
			branch("0") 
			{
				out(vec, a_position);
				out(nor, a_normal);
			}
		};

		
		highp float4 position = vec;
		highp float4 normal = nor;
		

		//mediump float4 uvxyz = float4(texcoord0.xy, 1.0f, 0.0f) * texTrans0;

		// 计算顶点位置
		highp float4 screen_pos = float4(transform(position, wvp));
		vertex(screen_pos);
		
		highp float4 pos_world = transform(position, world);
		
		float3x3 worldMat = float3x3(world);
		
		highp float3 world_normal = float3(normalize(transform(normal.xyz, worldMat)));

		//使用2U
		macro(UV002_ENABLE) out(mediump float2 UV02) 
		{
			branch ("1") 
			{
				out(UV02, texcoord1.xy);
			}
			branch ("0") 
			{
				out(UV02, texcoord0.xy);
			}
		};
		out(flow_uv, UV02);

		out(UV0, float4(texcoord0.xy, 0.0f, 0.0f));

		// 贴花UV
		// macro(DECAL_ENABLE){
		// 	branch("1"){
		// 		out(UV1, a_color.xy);
		// 	}
		// 	branch("0"){

		// 	}
		// };

		
		out(PosWorld, float4(pos_world.xyz, 1.0f));
		
		out(v_NormalWorld, world_normal);

		macro (XNORMAL_MAP_ENABLE)
		{
			branch("1") {
				mediump float3 tangentWorld = float3(normalize(a_tangent.xyz * worldMat));
		
				// 通过uv空间来判断是否是镜像模型，如果是镜像模型则反转法线
				//mediump float test_sign = float(sign(step(length(tangent.xyz), 1.0f) - 0.5f));
				//mediump float3 binormal_world = cross(world_normal, tangentWorld);
				//mediump float3 binormal_world = cross(world_normal, tangentWorld);
				highp float tan_coef = 1.0f - 2.0f * step(1.5f, length(a_tangent.xyz));
				mediump float3 binormal_world = cross(tangentWorld, world_normal) * tan_coef;

				out(v_TangentWorld, normalize(tangentWorld));
				out(v_BinormalWorld, normalize(binormal_world));
			}
			branch("0") {
			}
		};

		macro(LEVEL_MIDDLE)
		{
			branch ("1") 
			{
				macro(SHADOW_MAP_ENABLE)
				{
					branch("1")
					{				
						macro(POINT_SHADOW_MAP_ENABLE)
						{
							branch("1")
							{
								out(v_light_proj_pos, pos_world);

							}
							branch("0")
							{
								mediump float4 shadow_light_attr_3 = ShadowLightAttr[3];
								function calc_shadow_info(
									pos_world,
									world_normal,
									lvp,
									shadow_light_attr_3.xyz)
								out(
									highp float2 uv_out,
									highp float  depth_out,
									highp float  factor_out
								);

								out(v_light_proj_pos, float4(uv_out, depth_out, factor_out));

							}

						};
					}
					branch("0")
					{

					}				
				};
			}
			branch("0")
			{
			}
		};

		/*
		macro(DISSOLVE_ENABLE){
			branch ("1") {

				float2 dissolve_uv_screen = (screen_pos.xy / float2(screen_pos.w)) * float2(u_dissolve_tillingx,u_dissolve_tillingy);
				float2 dissolve_uv_model = UV02 * float2(u_dissolve_tillingx,u_dissolve_tillingy);
				float2 dissolve_uv = lerp(dissolve_uv_model,dissolve_uv_screen,float2(u_usescreen_uv));
				out(v_dissolve_uv, float4(dissolve_uv,0.0f,0.0f));
			}
			branch ("0") {
				
			}
		};
		*/
		// 角色高度
		// out(v_char_height, a_position.y/char_height);
	}



	pixel
	{
		//----------------流动贴图-------------------
		lowp float4 flowmap_color = sample(flowmap, flow_uv);
		lowp float sand_test = flowmap_color.z;
		lowp float body_test = abs(sand_test - 0.5f);
		condition(body_test) out(lowp float body_area) 
		{
			branch ("%0% < 0.002") 
			{
				out(body_area, 1.0f);
			}
			default 
			{
				out(body_area, 0.0f);
			}
		};

		lowp float sand_area = sand_test + 0.5f * body_area;
		
        
		

		macro(FLOW_CHECK) out(mediump float2 NewUV01, mediump float2 NewUV02, lowp float flow_lerp) {
			branch ("1") 
			{
				mediump float half_flow_period = flow_period / 2.0f;
	    		lowp float2 flow_direction00 = (flowmap_color.xy - float2(0.5f)) * 2.0f;
	    		lowp float2 flow_direction = flow_direction00 * float2(step(0.005f, abs(flow_direction00.x)), step(0.005f, abs(flow_direction00.y)));
	    		//lowp float2 flow_direction = flow_direction00;
	    		mediump float flow_time01 = fmod(FrameTime, flow_period);
	    		mediump float flow_time02 = fmod(FrameTime + half_flow_period, flow_period);

	    		out(NewUV01, flow_time01 * flow_speed * flow_direction + flow_uv);
	    		out(NewUV02, flow_time02 * flow_speed * flow_direction + flow_uv);
	    		out(flow_lerp, abs(half_flow_period - flow_time01) / half_flow_period);	
			}
			branch ("0") 
			{
			}
		};
        
		//lowp float4 flowmap_color_B = flowmap_color;
		macro(FLOW_CHECK) out(mediump float2 NewUV01B, mediump float2 NewUV02B, lowp float flow_lerp_B) {
			branch ("1") 
			{
				mediump float half_flow_period_B = flow_period_B / 2.0f;
	    		lowp float flow_direction_B00x = (flowmap_color.x - 0.5f) * 2.0f;
	    		lowp float2 flow_direction_B00 = float2(flow_direction_B00x, flow_direction_B00x * noise_fall_speed);
	    		lowp float2 flow_direction_B = flow_direction_B00 * float2(step(0.005f, abs(flow_direction_B00.x)), step(0.005f, abs(flow_direction_B00.y)));
	    		//lowp float2 flow_direction = flow_direction00;
	    		mediump float flow_time01_B = fmod(FrameTime, flow_period_B);
	    		mediump float flow_time02_B = fmod(FrameTime + half_flow_period_B, flow_period_B);

	    		out(NewUV01B, flow_time01_B * flow_speed_B * flow_direction_B + flow_uv);
	    		out(NewUV02B, flow_time02_B * flow_speed_B * flow_direction_B + flow_uv);
	    		out(flow_lerp_B, abs(half_flow_period_B - flow_time01_B) / half_flow_period_B);	
			}
			branch ("0") 
			{
			}
		};
		
		// —————————————————— 法线 ——————————————————
		/*	
		lowp float3 vertex_normal_world = v_NormalWorld;
		macro(XNORMAL_MAP_ENABLE) out(lowp float3 normalWorld, lowp float occlusion) {
			branch("1") {
				lowp float4 norm = sample(SamplerNormalMap, UV0.xy);
				macro(NORMAL_NOISE_CHECK) out(lowp float2 normalWorld_noise, lowp float2 normalWorld_wave) 
				{
					branch ("1") 
					{
						macro(FLOW_CHECK) out(lowp float2 norm_noise, lowp float2 norm_wave) 
						{
							branch ("1") 
							{
								lowp float4 norm_noise01 = sample(SamplerNormalMap_Add, NewUV01 * wave_noise_density);
				                lowp float4 norm_noise02 = sample(SamplerNormalMap_Add, NewUV02 * wave_noise_density);
				                lowp float2 norm_wave00 = norm_noise01.rg * (1.0f - flow_lerp) + norm_noise02.rg * flow_lerp;
				                lowp float norm_noise00 = norm_noise01.b * (1.0f - flow_lerp) + norm_noise02.b * flow_lerp;
				                out(norm_wave, norm_wave00);
				                //out(norm_noise, float2(norm_noise00, fractional(norm_noise00 * 129.457f)));
				                out(norm_noise, float2(norm_noise00));
							}
							branch ("0") 
							{
								lowp float4 norm_noise00 = sample(SamplerNormalMap_Add, flow_uv * wave_noise_density);
								out(norm_wave, norm_noise00.rg);
				                //out(norm_noise, float2(norm_noise00.b, fractional(norm_noise00.b * 129.457f)));
				                out(norm_noise, float2(norm_noise00.b));
							}
						};
						
				        out(normalWorld_noise, norm_noise * 2.0f - float2(1.0f));
				        out(normalWorld_wave, norm_wave * 2.0f - float2(1.0f));
					}
					branch ("0") 
					{
						out(normalWorld_noise, float2(0.0f));
						out(normalWorld_wave, float2(0.0f));
					}
				};

				lowp float2 normOff = (norm.xy * 2.0f - float2(1.0f)) + normalWorld_noise * sand_area * noise_normal_intensity + normalWorld_wave * (sand_area - body_area) * wave_normal_intensity;

				lowp float3 normalWorld_inner = normalize(v_TangentWorld * normOff.x + v_BinormalWorld * normOff.y + vertex_normal_world);
				out(normalWorld, normalWorld_inner);
				out(occlusion, norm.z);
			}	
			branch("0") {
				out(normalWorld, vertex_normal_world);
				out(occlusion, 1.0f);
			}
		};
		*/
		// —————————————————— 法线 ——————————————————	
		lowp float3 vertex_normal_world = v_NormalWorld;
		macro(XNORMAL_MAP_ENABLE) out(lowp float3 normalWorld, lowp float occlusion) {
			branch("1") {
				macro(FLOW_CHECK) out(lowp float4 norm) 
				{
					branch ("1") 
					{
						condition(sand_test) out(lowp float4 norm00) 
						{
							branch ("%0% > 0.995") 
							{
								lowp float4 norm01 = sample(SamplerNormalMap, NewUV01);
				                lowp float4 norm02 = sample(SamplerNormalMap, NewUV02);
				                out(norm00, norm01 * (1.0f - flow_lerp) + norm02 * flow_lerp);
							}
							default 
							{
								out(norm00, sample(SamplerNormalMap, UV0.xy));
							}
						};
						out(norm, norm00);
					}
					branch ("0") 
					{
						out(norm, sample(SamplerNormalMap, UV0.xy));
					}
				};
				//lowp float4 norm = sample(SamplerNormalMap, UV0.xy);
				macro(NORMAL_NOISE_CHECK) out(lowp float2 normalWorld_noise) 
				{
					branch ("1") 
					{
						macro(FLOW_CHECK) out(lowp float2 norm_noise) 
						{
							branch ("1") 
							{
								condition(sand_test) out(lowp float2 norm_noise00) 
						        {
							        branch ("%0% > 0.995") 
							        {
								        lowp float4 norm_noise01 = sample(SamplerNormalMap_Add, NewUV01B * noise_density);
				                        lowp float4 norm_noise02 = sample(SamplerNormalMap_Add, NewUV02B * noise_density);
				                        lowp float2 norm_noise000 = norm_noise01.rg * (1.0f - flow_lerp_B) + norm_noise02.rg * flow_lerp_B;
				                        //lowp float norm_noise00 = norm_noise01.b * (1.0f - flow_lerp) + norm_noise02.b * flow_lerp;
				                        //out(norm_wave, norm_wave00);
				                        //out(norm_noise, float2(norm_noise00, abs(norm_noise00 - 0.7f)));
				                        //out(norm_noise, float2(norm_noise00));
				                        out(norm_noise00, norm_noise000);
				                    }
				                    default 
							        {
								        out(norm_noise00, (sample(SamplerNormalMap_Add, flow_uv * noise_density)).rg);
							        }
				                };
				                out(norm_noise, norm_noise00);
							}
							branch ("0") 
							{
								lowp float4 norm_noise00 = sample(SamplerNormalMap_Add, flow_uv * noise_density);
								//out(norm_wave, norm_noise00.rg);
				                out(norm_noise, norm_noise00.rg);
				                //out(norm_noise, float2(norm_noise00.b));
							}
						};
						
				        out(normalWorld_noise, norm_noise * 2.0f - float2(1.0f));
				        //out(normalWorld_wave, norm_wave * 2.0f - float2(1.0f));
					}
					branch ("0") 
					{
						out(normalWorld_noise, float2(0.0f));
						//out(normalWorld_wave, float2(0.0f));
					}
				};

				lowp float2 normOff = lerp(1.0f, flow_normal_intensity, sand_area - body_area) * (norm.xy * 2.0f - float2(1.0f)) + normalWorld_noise * sand_area * noise_normal_intensity;

				lowp float3 normalWorld_inner = normalize(v_TangentWorld * normOff.x + v_BinormalWorld * normOff.y + vertex_normal_world);
				//lowp float3 normalWorld_inner = normalize(v_TangentWorld * float3(normOff.x, normOff.x, 0.0f) + v_BinormalWorld * float3(normOff.y, normOff.y, 0.0f) + vertex_normal_world);
				out(normalWorld, normalWorld_inner);
				out(occlusion, norm.z);
			}	
			branch("0") {
				out(normalWorld, vertex_normal_world);
				out(occlusion, 1.0f);
			}
		};

		// —————————————————— 法线噪声与流动 ——————————————————
		/*macro(NORMAL_NOISE_CHECK) out(lowp float3 normalWorld) 
		{
			branch ("1") 
			{
				macro(FLOW_CHECK) out(lowp float3 normalWorld_noise) 
				{
					branch ("1") 
					{
						lowp float normal_noise01 = (sample(flowmap, NewUV01)).z;
				        lowp float normal_noise02 = (sample(flowmap, NewUV02)).z;
				        lowp float normal_noise00 = normal_noise01 * (1.0f - flow_lerp) + normal_noise02 * flow_lerp;
				        lowp float normal_noise = normal_noise00 * 2.0f - 1.0f;
				        out(normalWorld_noise, normalize(float3(normalWorld0.xy,normal_noise * normal_noise_intensity + normalWorld0.z)));
					}
					branch ("0")
					{
						lowp float normal_noise = 2.0f * (sample(flowmap, UV0.xy)).z - 1.0f;
						out(normalWorld_noise, normalize(float3(normalWorld0.xy,normal_noise * normal_noise_intensity + normalWorld0.z)));
					}
				};
				
				out(normalWorld, normalWorld_noise);
			}
			branch ("0") 
			{
				out(normalWorld, normalWorld0);
			}
		};*/		

		// —————————————————— 自投影(shadow_fac计算) ——————————————————	
		macro(LEVEL_MIDDLE) out(mediump float shadow_fac_) 
		{
			branch ("1") 
			{
				macro(SHADOW_MAP_ENABLE) out (mediump float shadow_fac_0)
				{
					branch("1") {         
						macro(POINT_SHADOW_MAP_ENABLE) out(mediump float fac)
						{
							branch("0")
							{
								mediump float4 v_light_proj_pos_ = v_light_proj_pos;
								macro(LEVEL_HIGH) out(mediump float fac0) 
								{
									branch ("1") 
									{
										function calc_shadowmap_factor_sampling9(SMAP, v_light_proj_pos_, u_shadowmap_info, shadow_bias_factor * shadowmap_bias, shadowmap_uv_bias)
								        out(lowp float factor);
								        out(fac0, factor);
									}
									branch ("0") 
									{
										function calc_shadowmap_factor_sampling5(SMAP, v_light_proj_pos_, u_shadowmap_info, shadow_bias_factor * shadowmap_bias, shadowmap_uv_bias)
								        out(lowp float factor);
								        out(fac0, factor);
									}
								};
								
							    // 自投影的浓度为0.8
						        // out(shadow_fac_, lerp(1.0f,factor,0.8f));
								out(fac, fac0);
							}
							branch("1")
							{	
								mediump float4 shadow_light_attr_3 = ShadowLightAttr[3];
								function calc_point_shadow_info(
									v_light_proj_pos,
									vertex_normal_world,
									lvp,
									lv,
									shadow_light_attr_3.xyz)
								out(
									highp float2 uv_out,
									highp float  depth_out,
									highp float  factor_out
								);
								float4 cal_pos = float4(uv_out, depth_out, factor_out);
								function calc_shadowmap_factor(SMAP, cal_pos, u_shadowmap_info, shadow_bias_factor)
								out(lowp float factor);
							// 自投影的浓度为0.8
						// out(shadow_fac_, lerp(1.0f,factor,0.8f));
								out(fac, factor);							
							}
						};
						out(shadow_fac_0, fac);
					}
					branch("0") {
						out(shadow_fac_0, 1.0f);
					}
				};
				out(shadow_fac_, shadow_fac_0);
			}
			branch("0")
			{
				out(shadow_fac_, 1.0f);
			}
		};


		// ——————————————————★★★ 基础设置 ★★★——————————————————	
		// 采样贴图
	    macro(FLOW_CHECK) out(lowp float4 diffuse_map_color_) {
	    	branch ("1") 
	    	{
	    		condition(sand_test) out(lowp float4 diffuse_map_color_0) 
	    		{
	    			branch ("%0% > 0.995") 
	    			{
	    				lowp float4 diffuse_map_color_flow01 = sample(Tex0, NewUV01);
	    		        lowp float4 diffuse_map_color_flow02 = sample(Tex0, NewUV02);

	    		        out(diffuse_map_color_0, diffuse_map_color_flow01 * (1.0f - flow_lerp) + diffuse_map_color_flow02 * flow_lerp);

	    		        //out(diffuse_map_color_0, sample(Tex0, UV0.xy));
	    			}
	    			default 
	    			{
	    				out(diffuse_map_color_0, sample(Tex0, UV0.xy));
	    			}
	    		};
	    	    
	    		out(diffuse_map_color_, diffuse_map_color_0);

	    	}
	    	branch ("0") 
	    	{
	    		out(diffuse_map_color_, sample(Tex0, UV0.xy));
	    	}
	    };

	    //lowp float4 diffuse_map_color_ = sample(Tex0, UV0.xy);

		macro(LEVEL_MIDDLE) out(lowp float4 diffuse_map_color)
		{
			branch("1")
			{
				macro(DIFFUSE_ALPHA_SEPARATE) out(lowp float4 diffuse_map_color0)
				{
					branch("1")
					{
						lowp float4 separateColor = sample(sepAlphaTex, UV0.xy);
						out(diffuse_map_color0, float4(diffuse_map_color_.xyz, separateColor.w));
					}
					branch("0") {
						out(diffuse_map_color0, diffuse_map_color_);
					}
				};
				out(diffuse_map_color,diffuse_map_color0);
			}
			branch("0")
			{
				out(diffuse_map_color,diffuse_map_color_);
			}
		};

		lowp float4 pbr_map = sample(SamplerRough, UV0.xy);
		lowp float metal = saturate(pbr_map.r + metal_multi);
		lowp float rough = saturate(pbr_map.b + rough_multi);

		// ——————————————————★★★ 光照设置 ★★★——————————————————
		macro(POINT_LIGHT_ENABLE) out(mediump float3 gi_color, mediump float3 light_dir) {
			branch ("1") {
				// 点光颜色
				lowp float4 light_color = ShadowLightAttr[1];
				mediump float4 dir_vector = ShadowLightAttr[3];
				lowp float light_range = dir_vector.w;
				mediump float3 light_dir_ = PosWorld.xyz - dir_vector.xyz; 
				out(light_dir, normalize(light_dir_)); 
				lowp float dist = length(light_dir_);
				lowp float point_light_fac = clamp((light_range - dist) / light_range, 0.0f, 1.0f);
				out(gi_color, lerp(dark_color.rgb, light_color.rgb, float3(point_light_fac * point_light_fac)));
			}
			branch ("0"){
				//平行光颜色
				lowp float4 light_color = ShadowLightAttr[1]; 
				out(gi_color, light_color.xyz); 

				// 光源方向
				mediump float4 light_dir_ = ShadowLightAttr[3];
				out(light_dir, normalize(light_dir_.xyz));
				//mediump float3 light_dir = u_light_scale * normalize(float3(u_light_pos_x, u_light_pos_y, u_light_pos_z));
			}

		};

		lowp float3 light_dir_ndl = normalize(float3(light_dir.x, light_dir.y, light_dir.z));

		// 视线方向
		lowp float3 view_dir = normalize(camera_pos.xyz - PosWorld.xyz);

		// ——————————————————★★★ 常用参数 ★★★——————————————————

		lowp float roughness = rough;	
				
		lowp float non_metal = 1.0f - metal;

		lowp float3 H = normalize(view_dir - light_dir_ndl);
		mediump float VoH = dot(view_dir, H);
		mediump float VoL = dot(view_dir, -light_dir_ndl);
		mediump float NoV = dot(view_dir, normalWorld);
		mediump float VdotH = saturate(VoH);
		mediump float NoH = dot(normalWorld, H);
		mediump float NdotH = saturate(NoH);
		mediump float NdotV = saturate(NoV);

		// NdotL
		mediump float NoL = dot(normalWorld, -light_dir_ndl);

		// 计算OrenNayarLighting因子
		lowp float ggx_rough = lerp(0.04f, 1.0f, rough);

		macro(LEVEL_MIDDLE) out(lowp float orennayar_factor) 
		{
			branch ("1") 
			{
				macro(ORENNAYAR_ENABLE) out(lowp float orennayar_factor0) {
					branch ("0") {
						out(orennayar_factor0,1.0f);
					}
					branch ("1") {
						float a = ggx_rough * ggx_rough;
				        float s = a;// / ( 1.29 + 0.5 * a );
						float s2 = s * s;
				        float VoL1 = 2.0f * VoH * VoH - 1.0f;		// double angle identity
						float Cosri = VoL1 - NoV * NoL;
						float C1 = 1.0f - 0.5f * s2 / (s2 + 0.33f);
						condition(Cosri) out(float C3) {
							branch ("%0% >= 0.0") {
								out(C3, min(1.0f,NoL/max(0.01f, NoV)));
							}
							default {
								out(C3, 1.0f);
							}
						};
						float C2 = 0.45f * s2 / (s2 + 0.09f) * Cosri * C3;
						out(orennayar_factor0, clamp(( C1 + C2 ) * ( 1.0f + ggx_rough * 0.5f), 0.0f, 5.0f));
					}
				};
				out(orennayar_factor, orennayar_factor0);
			}
			branch("0")
			{
				out(orennayar_factor, 1.0f);
			}
		};
		

		["计算暗部补充光线"]
		// mediump float3 fix_normal = normalize(vertex_normal_world + 0.1f * view_dir);
		// mediump float3 fix_normal = normalize(normalWorld + 0.1f * view_dir);
		// mediump float fix_nol = saturate(dot(fix_normal, light_dir_ndl));
		// mediump float light_view_dir = transform(light_dir_ndl, float3x3(view_mat)).b;
		// mediump float ambient_fix = 0.5f * fix_nol *fix_nol * (0.2f + saturate(light_view_dir + 0.5f));
		mediump float ambient_fix = 0.0f;//关闭暗部补光
		
		// mediump float NdotL_spec = min(saturate(NoL),shadow_fac_);// 此为specular的nol
		mediump float NdotL_spec = min(saturate(NoL),shadow_fac_);
		lowp float NdotL_diff = NdotL_spec * orennayar_factor;

		// ——————————————————★★★ DIFFUSE ★★★—————————————————— 
		mediump float3 diffuse_color = diffuse_map_color.xyz * diffuse_map_color.xyz * diffuse_intensity; 
				

		// 金属度计算过后的漫反射光强
		mediump float3 non_metal_diffuse = non_metal * diffuse_color; 

		// 方向环境光 		
		macro (DIR_AMBIENT_ENABLE) out (mediump float3 dir_ambient_color)
		{
			branch("1"){
				mediump float3x3 inverse_view_mat = float3x3(inverse_view);
				//mediump float dir_ambient_fac = clamp(-normalWorld.y, 0.0f, 1.0f);
				mediump float3 ambient_dir = normalize(float3(l_pos_x, l_pos_y, l_pos_z) * inverse_view_mat);
				lowp float dir_ambient_fac_ = clamp(dot(normalWorld, ambient_dir), 0.0f, 1.0f);
				lowp float dir_ambient_oc = lerp(1.0f,NdotL_spec,dir_ambient_occlusion);
				out(dir_ambient_color, dir_ambient.rgb * dir_ambient_fac_ * dir_ambient_intensity * (0.5f * non_metal + 0.5f) * dir_ambient_oc);
			}
			branch("0"){
				out(dir_ambient_color, float3(0.0f));	

			}
		};

		// AO
		macro(ENVIR_ENABLE) out(lowp float3 AO_color) {
			branch ("1") {
				lowp float _AO_adjust = lerp(1.0f, occlusion ,NdotV);
				out(AO_color, float3(lerp(_AO_adjust, occlusion,2.0f * saturate(0.5f - occlusion))));
			}
			branch ("0") {
				out(AO_color, float3(1.0f));
			}
		};


		// macro(ENVIR_ENABLE) out(mediump float3 R_rotated) {
		// 	branch ("1") {
		// 		mediump float3 R = reflect(-view_dir, normalWorld);
		// 		out(R_rotated, R);
		// 	}
		// 	branch("0"){
		// 		out(R_rotated, float3(0.0f));
		// 	}
		// };

		// 增加半球光照
		// lowp float3 sky_light = saturate(normalWorld.g  + skylight_smooth) * sky_light_color.rgb * sky_light_intensity * saturate(lerp(1.0f,PosWorld.y / 20.0f, sky_light_height));
		// 增加高度渐变以及垂直范围的变化
		// lowp float3 sky_light = saturate(normalWorld.g  + 0.35f) * sky_light_color.rgb * sky_light_intensity * saturate(lerp(1.0f,PosWorld.y / 20.0f, 0.31f));
		lowp float3 sky_light = float3(0.0f);

		// SH
		// function calc_env_sh( envSHR, envSHG, envSHB, float3(normal_view.x, normal_view.y,-normal_view.z)) out (mediump float3 ibl_diffuse);
		function calc_env_sh( envSHR, envSHG, envSHB, normalWorld) out (mediump float3 ibl_diffuse1);
		mediump float3 ibl_diffuse = ibl_diffuse1 * 3.0f; //环境光的sh与高光比为3:130;

		// 环境光调整色计算
		// lowp float3 env_factor_color = lerp(env_shadow_factor.rgb ,float3(1.0f), float3(NdotL_diff));
		lowp float3 env_factor_color = env_shadow_factor.rgb * (1.0f + ambient_fix) + NdotL_diff * gi_color * 0.5f; 
		// lowp float3 env_factor_color = env_shadow_factor.rgb ; 

		// Final Diffuse
		// 固有色 * (主光源 + 环境光 + 天光) + 补光
		mediump float3 final_diffuse = non_metal_diffuse * ((NdotL_diff + ambient_fix) * gi_color + ibl_diffuse * (sky_light + env_factor_color * envir_brightness)) + dir_ambient_color;

		// —————————————————— ★★★ SPECULAR ★★★—————————————————— 
		mediump float3 spec_color = lerp(diffuse_color.xyz, float3(0.04f), float3(non_metal)); 

		// F
		function F_Schlick(spec_color,VdotH) out (lowp float3 F);	
		// D 
		function D_GGX(ggx_rough, NdotH) out(lowp float D);
		
		// G
		function G_Schlick_Disney(ggx_rough, NdotV, NdotL_spec) out (lowp float G);
		// specular BRDF
		//mediump float3 brdf =  D * spec_color * spec_intensity;

		mediump float3 brdf =  D * F * G  ;

		// Final Specular

		mediump float3 final_specular = brdf * NdotL_spec * gi_color;


		// ——————————————————★★★ IBL ★★★—————————————————— 
		macro(LEVEL_MIDDLE) out(mediump float3 final_envir) 
		{
			branch ("1") 
			{
				macro(ENVIR_ENABLE) out(mediump float3 final_envir0)
				{
					branch("1") {
						mediump float3 R_rotated = reflect(-view_dir, normalWorld);
						function GetEnvirmentColor_panorama (SamplerCubeMap, roughness, R_rotated, 130.0f) out (mediump float3 envir); // 环境光最大强度不改了，再改是猪！
						function EnvBRDFApprox(spec_color, ggx_rough, NdotV, oneminus(NdotL_spec * 0.8f) * envir_fresnel_brightness) out (mediump float3 env_brdf);
						out(final_envir0,  env_brdf * envir * (sky_light + env_factor_color * envir_brightness));
					}
					branch("0") {
						out(final_envir0, diffuse_color * ibl_diffuse * metal * lerp(0.6f, 1.0f, NdotH));
					}
				};
				out(final_envir,  final_envir0);
			}
			branch("0")
			{
				mediump float3 normal_view = normalize(transform(normalWorld, float3x3(wv)));
				mediump float2 matcap_uv = (normal_view.xy) * float2(0.5f, -0.5f) + float2(0.5f);
				lowp float4 matcap_color = sample(MatcapTex, matcap_uv);
				lowp float matcap_intensity01 = lerp(matcap_color.r, matcap_color.g, rough * 2.0f); //rough<0.5
				lowp float matcap_intensity02 = lerp(matcap_color.g, matcap_color.b, rough * 2.0f - 1.0f); //rough>0.5
				lowp float overhalf = step(0.5f, rough);
				lowp float matcap_final = (1.0f - overhalf) * matcap_intensity01 + overhalf * matcap_intensity02;
				//mediump float3 final_envir0 = float3(matcap_final) * lerp(float3(1.0f), float3(0.4f, 0.6f, 1.0f), float3(0.3f + normal_view.y));
                mediump float3 final_envir1 = float3(matcap_final) * ibl_diffuse * lerp(float3(1.0f), ibl_diffuse, float3(dot(normal_view.xy, normal_view.xy)));
                out(final_envir, 2.0f * diffuse_color * final_envir1 * envir_brightness * metal * lerp(0.6f, 1.0f, NdotH));
			}
		};

		// ——————————————————★★★ 自发光 & 半透 ★★★—————————————————— 
		lowp float arufa = diffuse_map_color.a; 
		macro(SELF_BLING) out (mediump float3 final_emissive, lowp float emissive_area){
			branch("1"){
				mediump float3 final_emissive_ = arufa * emissive_intensity * non_metal_diffuse.rgb;

				out(final_emissive, final_emissive_);
				out(emissive_area, arufa);
				// out(diffuse_alpha, _alpha);
			}
			branch("0"){
				out(final_emissive, float3(0.0f));
				out(emissive_area, 0.0f);
				// out(diffuse_alpha, diffuse_map_color.a);
			}
		};

		lowp float diffuse_alpha = 1.0f; //取消半透明功能


		/////////////////////////////////////////////////////////////////
		// ——————————————————★★★ 最终光照计算 ★★★——————————————————
		/////////////////////////////////////////////////////////////////
		// pbr的最终计算结果 = (漫反射 + 动态光反射 + 环境反射）* 主光颜色 * AO  （IBL也要受主光颜色影响，这样不会出现与主光色调不一致的情况）

		mediump float3 combine_spec = final_specular + final_envir ;
		mediump float3 pbr_light_color =  (final_diffuse + combine_spec) * AO_color;
		mediump float3 result_diff_raw =  pbr_light_color + final_emissive;

		// —————————————————— 边缘光 ———————————————————
		macro(LEVEL_MIDDLE) out(mediump float3 fresnel_combine)
		{
			branch("1")
			{
				
				macro(WEB_SHINING_ENABLE) out(mediump float3 fresnel_combine0) 
				{
					branch ("0") 
					{
						out(fresnel_combine0,result_diff_raw);
					}
					branch ("1") 
					{
						["网格闪烁消融效果"]
						function web_shining
						(
					burn_max_height, //角色身高
					burn_color01,   //闪烁颜色(较暗)
					burn_color02,	  //闪烁颜色(较亮)
					burn_style,		 //消融方向控制
					PosWorld.xyz,	   //当前像素位置
					(world[3]).xyz,   //角色位置 
					ChangeAmount,	   //消融量
					result_diff_raw   //当前颜色 
						)
						out(lowp float3 final_shining_color, lowp float alphatest01, lowp float alphatest02);

						alphatest("<")(alphatest01, alphatest02);
						out(fresnel_combine0,final_shining_color.rgb);
					}
				};
				
				out(fresnel_combine, fresnel_combine0);
			}
			branch("0")
			{
				out(fresnel_combine, result_diff_raw);
			}
		};

		lowp float emissive_bloom1 = emissive_bloom;
		lowp float tmp = bloom_range + illum_multi; // 只是为了美术同学可以调整

		float web_light = dot(abs(fresnel_combine - result_diff_raw), float3(0.3f, 0.59f, 0.11f)); // 网格效果辉光
				
		function unreal_tonemapping(fresnel_combine, ToneFactor, 0.88f, 0.55f, 0.26f, 0.00f, 0.04f) out(float3 ToneColor_aces);
		
		function Luminance(combine_spec) out (mediump float color_illum);
		lowp float alphchannel = saturate((color_illum - bloom_threshold)) * clamp(metal+bloom_range, 0.0f, 1.0f) * illum_multi + final_emissive.r * emissive_bloom1 + web_light * webBloom;
		lowp float4 encode_color_1 = float4(ToneColor_aces, alphchannel);

		// —————————————————— diffuse check ———————————————————
		macro(DIFFUSE_CHECK) out(mediump float4 diffuse_check){
			branch("1"){
				out(diffuse_check, diffuse_map_color);
			}
			branch("0"){
				out(diffuse_check, float4(encode_color_1));
			}
		};
		
		macro(ROUGHNESS_CHECK) out(lowp float4 final_color1)
		{
			branch ("0") {
				out(final_color1, diffuse_check);
			}
			branch ("1") {
				out(final_color1, float4(ggx_rough,ggx_rough,ggx_rough,1.0f));
			}
		};

		macro(METAL_CHECK) out(lowp float4 final_color2)
		{
			branch ("0") {
				out(final_color2, final_color1);
			}
			branch ("1") {
				out(final_color2, float4(metal,metal,metal,1.0f));
			}
		};

		macro(AO_CHECK) out(lowp float4 final_color3)
		{
			branch ("0") {
				out(final_color3, final_color2);
			}
			branch ("1") {
				out(final_color3, float4(occlusion));
			}
		};
		// 支持硬边溶解、柔边溶解、溶解边缘发光
		/*
		macro(DISSOLVE_ENABLE) out(lowp float3 mix_color, lowp float mix_alpha) {
			branch("1")
			{
				// float2 dissolve_tilting = u_shader_attributes.xy; 
				highp float3 dissolve_color = sample(u_tex_dissolve, v_dissolve_uv.xy).rgb;
				macro(SOFTEDGE_ENABLE) out(lowp float dissolve_fator) {
					branch ("0") {
						alphatest("<")(dissolve_color.r - u_dissolve_intensity, 0.0f);
						out(dissolve_fator,step(u_dissolve_intensity,dissolve_color.r));
					}
					branch ("1") {
						lowp float fixed_dissolve_edge = max(0.01f, u_dissolve_edge);
						function linearstep(u_dissolve_intensity,saturate(u_dissolve_intensity + fixed_dissolve_edge),dissolve_color.r) out (lowp float _dissolve_fator);
						out(dissolve_fator,_dissolve_fator);
					}
				};

				function linearstep(u_dissolve_intensity - u_gloss_edge, u_dissolve_intensity + u_gloss_edge, dissolve_color.r) out (lowp float _gloss_edge_factor);
				mediump float gloss_edge_factor = 1.0f - _gloss_edge_factor;
				lowp float3 mixed_gloss_edge_color = lerp(final_color3.rgb, u_dissolve_color.rgb, float3(saturate(u_gloss_strength * gloss_edge_factor)));
				lowp float mixed_gloss_edge_alpha = (final_color3.a + gloss_edge_factor) * dissolve_fator;

				out(mix_color, mixed_gloss_edge_color.rgb);
				out(mix_alpha, mixed_gloss_edge_alpha);
			}
			branch("0") {
				out(mix_color, final_color3.rgb);
				out(mix_alpha, final_color3.a);
			}
		};
		*/

		pixel(final_color3);
		//pixel(float4(sand_test, sand_test, sand_test, 1.0f));
		 // pixel(float4(NdotL_eye));
		 //pixel(float4(encode_color, alpha_fresnel * diffuse_alpha * AlphaMtl));
		//pixel(float4(float3(normalWorld), 1.0f));
		//
		//E:\SVN\tools_new\shaderpuzzle_publish_g104\SPScriptCompiler.exe -i E:\SVN\G104trunk\shader_source\shader\vbr_sand.spzs -o E:\SVN\G104trunk\res\shader -r E:\SVN\G104trunk\shader_source --glsl es300
	}
}

